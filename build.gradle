plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.22.1'
    id 'application'
    id 'maven-publish'
}

apply from: 'versions.gradle'

wrapper {
    gradleVersion = '6.6.1'
}

description = "The official launcher for the Oldschool RuneScape Spectral client."

group = 'org.spectral.launcher'
version = '1.0.0'

javafx {
    version = '14'
    modules = ['javafx.base', 'javafx.fxml', 'javafx.web', 'javafx.media', 'javafx.controls', 'javafx.graphics', 'javafx.swing']
}

run {
    mainClassName = "org.spectral.launcher.gui.LauncherApp"
    workingDir = rootProject.projectDir
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.tinylog:tinylog-api-kotlin:$tinylog"
    implementation "org.tinylog:tinylog-impl:$tinylog"
    implementation "javax.xml.bind:jaxb-api:$jaxb"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxb"
    implementation "no.tornado:tornadofx:$tornadofx"
}
repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_14.toString()
    }
}

compileJava {
    targetCompatibility = JavaVersion.VERSION_14.toString()
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Spectral Client'
    }
    addExtraDependencies("javafx")
    jpackage {
        imageOptions += ['--icon', 'src/main/resources/icons/windows.ico']
        installerOptions += [
                '--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut'
        ]
    }
}

publishing {
    repositories {
        mavenLocal()
        maven {
            url 'https://repo.spectralpowered.org'
            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = "org.spectral.launcher"
            artifactId = "launcher"
            version = rootProject.version
            from components.java
        }
    }
}
